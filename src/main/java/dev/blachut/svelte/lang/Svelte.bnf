{
  generate=[
    java="8"
    names="long"
//    token-sets="yes"
//    psi-classes-map="yes"
  ]

//  generateTokenAccessors=true

  parserClass="dev.blachut.svelte.lang.SvelteParser"

  extends="dev.blachut.svelte.lang.psi.SveltePsiElementImpl"

  psiClassPrefix="Svelte"
  psiImplClassSuffix="Impl"
  psiPackage="dev.blachut.svelte.lang.psi"
  psiImplPackage="dev.blachut.svelte.lang.psi.impl"

  elementTypeHolderClass="dev.blachut.svelte.lang.psi.SvelteTypes"
  elementTypeClass="dev.blachut.svelte.lang.psi.SvelteElementType"
  tokenTypeClass="dev.blachut.svelte.lang.psi.SvelteElementType"

  tokens = [
    START_OPENING_MUSTACHE="{#"
    START_INNER_MUSTACHE="{:"
    START_CLOSING_MUSTACHE="{/"
    START_MUSTACHE="{"
    END_MUSTACHE="}"
    IF="if"
    EACH="each"
    AS="as"
    COMMA=","
    START_PAREN="("
    END_PAREN=")"
    AWAIT="await"
    THEN="then"
    CATCH="catch"
    ELSE="else"
    HTML_FRAGMENT="<markup>"
    HTML_PREFIX="@html"
    DEBUG_PREFIX="@debug"
  ]

  extends(".*Block")=block
  extends(".*OpeningTag")=openingTag
  extends(".*ContinuationTag")=continuationTag
  extends(".*ClosingTag")=closingTag

  pin("awaitThenBlockOpeningTag")=4
  pin("elseIfContinuationTag")=3
  pin(".*Tag")=2
  pin("interpolation")=1
  recoverWhile(".*Tag|interpolation")=mustache_recover
}

// First rule is always implicitly private
svelteComponent ::= privateScope
scope ::= privateScope

private mustache_recover ::= !('{' | '{#' | "{:" | "{/" | HTML_FRAGMENT)

private privateScope ::= (block|interpolation|HTML_FRAGMENT)*

interpolation ::= '{' expressionPrefix? expression '}'
private expressionPrefix ::= '@html' | '@debug'

block ::= ifBlock | eachBlock | awaitBlock

ifBlock ::= ifBlockOpening elseIfContinuation* elseContinuation? ifBlockClosingTag {pin=1}
ifBlockOpening ::= ifBlockOpeningTag scope
ifBlockOpeningTag ::= '{#' 'if' expression '}'
elseIfContinuation ::= elseIfContinuationTag scope {name="else if block"}
elseIfContinuationTag ::= '{:' 'else' 'if' expression '}'
ifBlockClosingTag ::= '{/' 'if' '}'

eachBlock ::= eachBlockOpening elseContinuation? eachBlockClosingTag {pin=1}
eachBlockOpening ::= eachBlockOpeningTag scope
eachBlockOpeningTag ::= '{#' 'each' expression 'as' parameter (',' parameter)? ('(' expression ')')? '}'
eachBlockClosingTag ::= '{/' 'each' '}'

awaitBlock ::= (awaitThenBlockOpening | awaitAndThen) (catchContinuation)? awaitBlockClosingTag {pin=1}
private awaitAndThen ::= awaitBlockOpening thenContinuation {pin=1}
awaitThenBlockOpening ::= awaitThenBlockOpeningTag scope
awaitThenBlockOpeningTag ::= '{#' 'await' expression 'then' parameter '}'
awaitBlockOpening ::= awaitBlockOpeningTag scope
awaitBlockOpeningTag ::= '{#' 'await' expression '}'
thenContinuation ::= thenContinuationTag scope {name="then block"}
thenContinuationTag ::= '{:' 'then' parameter '}'
catchContinuation ::= catchContinuationTag scope {name="catch block"}
catchContinuationTag::= '{:' 'catch' parameter '}'
awaitBlockClosingTag ::= '{/' 'await' '}'

elseContinuation ::= elseContinuationTag scope {name="else block"}
elseContinuationTag ::= '{:' 'else' '}'

fake openingTag ::= '{#' foo '}'
fake continuationTag ::= '{:' foo '}'
fake closingTag ::= '{/' foo '}'

expression ::= CODE_FRAGMENT {
                               name="expression"
                               extends="dev.blachut.svelte.lang.psi.SvelteCodeInjectionHostImpl"
                               implements="com.intellij.psi.PsiLanguageInjectionHost"
                             }
parameter ::= CODE_FRAGMENT {
                              name="parameter"
                              extends="dev.blachut.svelte.lang.psi.SvelteCodeInjectionHostImpl"
                              implements="com.intellij.psi.PsiLanguageInjectionHost"
                            }